#Boolean network from Sizek et al 2019 PLOS Comp Bio. Received from Herbert Sizek 2020. 
#Sizek, H., Hamel, A., Deritei, D., Campbell, S., and Ravasz Regan, E. (2019). Boolean model of growth signaling, cell cycle and apoptosis predicts the molecular mechanism of aberrant cell cycle progression driven by hyperactive PI3K. PLoS Comput Biol 15, e1006402.
#BOOLEAN RULES
GF *= GF or GF_High
RTK *= not CAD and (GF_High or GF)
Grb2 *= RTK and GF_High
Ras *= Grb2 and SOS
RAF *= not Casp3 and Ras
mTORC2 *= PIP3 or not S6K
PI3K *= Ras or RTK
PIP3 *= PI3K_H or PI3K
PDK1 *= PI3K and PIP3
AKT_B *= not Casp3 and PIP3 and (PDK1 or mTORC2)
p110_H *= (FoxO3 and not NeddL4) or (p110_H and (FoxO3 or not NeddL4) )
PI3K_H *= p110_H and RTK and PI3K and Ras
AKT_H *= AKT_B and p110_H and PI3K_H and Ras and PIP3 and PDK1 and mTORC2
FoxO3 *= not (AKT_B or AKT_H or ERK) or ( not (AKT_H and (Plk1 or Plk1_H or AKT_B or ERK) ) and not (Plk1 and Plk1_H and ERK) )
PLCgamma *= RTK and Grb2 and p110_H and PI3K_H and PIP3
NeddL4 *= Ca2 and IP3
FoxO1 *= not Plk1 and not AKT_H
p21_mRNA *= (FoxO1 and FoxO3) or (not Myc and (FoxO1 or FoxO3) )
TSC2 *= not AKT_H or not (AKT_B or ERK)
PRAS40 *= not AKT_H and (not mTORC1 or not AKT_B)
Rheb *= not TSC2 and DAG
mTORC1 *= not Casp3 and ( (Rheb and not PRAS40) or E2F1 or (CyclinB and Cdk1 and GSK3) )
S6K *= not Casp3 and mTORC1
eIF4E *= not Casp3 and mTORC1
GSK3 *=  not AKT_H and not (S6K and ERK)
p21 *= p21_mRNA and not Casp3 and not CyclinE
pRB *= not Casp3 and not CyclinD1 and not CyclinA and (p27Kip1 or not CyclinE)
p27Kip1 *= not Casp3 and not CyclinD1 and not (Cdk1 and CyclinB) and ( (not CyclinE and (FoxO3 and FoxO1) ) or (not CyclinA and (FoxO3 or FoxO1) ) or (not CyclinE and not CyclinA) )
Myc *= (ERK and (eIF4E or not GSK3) ) or ( E2F1 and not pRB and (eIF4E or ERK or not GSK3) )
CyclinD1 *= not CHK1 and ( ( not p21 and ( (not GSK3 and (Myc or E2F1) ) or (Myc and CyclinD1) or (Myc and E2F1) or (E2F1 and CyclinD1) ) ) or ( not pRB and E2F1 and ( (Myc and CyclinD1) or (Myc and not GSK3) or (CyclinD1 and not GSK3) ) ) )
E2F1 *= not (CAD or CyclinA or pRB) and (E2F1 or Myc)
CyclinE *= E2F1 and Cdc6 and Pre_RC and not (pRB or p27Kip1 or CHK1 or Casp3)
ORC *= E2F1 or (Pre_RC and Cdt1 and Cdc6)
Cdc6 *= not Casp3 and not (f4N_DNA and CyclinA) and ( (E2F1 and ORC and not Plk1) or (Pre_RC and ORC and Cdc6 and Cdt1) )
Cdt1 *= not geminin and ORC and Cdc6 and not (CyclinE and CyclinA and Cdc25A) and ( (Pre_RC and (E2F1 or Myc) ) or (E2F1 and (Myc or not pRB) ) )
Pre_RC *= ORC and Cdc6 and Cdt1 and not (Replication and f4N_DNA)
geminin *= E2F1 and not Cdh1 and not (pAPC and Cdc20)
CyclinA_mRNA *= not CAD and ( (E2F1 and not pRB) or FoxM1 )
Emi1 *= (E2F1 or not pRB or not p21) and not (Plk1 and CyclinB and Cdk1 and (U_Kinetochores or A_Kinetochores) )
FoxM1 *= (Myc and CyclinE) or (CyclinA and Cdc25A and Cdc25B) or (Plk1 and CyclinB and Cdk1)
Cdc25A *= ( (FoxM1 and E2F1 and not pRB) or (not Cdh1 and (FoxM1 or (E2F1 and not pRB) ) ) ) and (not (GSK3 or CHK1) or CyclinE or CyclinA or (CyclinB and Cdk1) )                   
CyclinA *= CyclinA_mRNA and not pAPC and ( (Cdc25A and (not Cdh1 or Emi1) ) or (CyclinA and ( (not Cdh1 and (Emi1 or not UbcH10) ) or (Emi1 and not UbcH10) ) ) )
Wee1 *= not Casp3 and not (Cdk1 and CyclinB) and (Replication or CHK1) and (CHK1 or not (Cdk1 and CyclinA and Plk1) )
UbcH10 *= not Cdh1 or (UbcH10 and (Cdc20 or CyclinA or CyclinB) )
CyclinB *= (FoxM1 or (FoxO3 and CyclinB) ) and not (Cdh1 or (pAPC and Cdc20) )
Cdc25B *= FoxM1 and f4N_DNA
Plk1 *= not Cdh1 and (FoxM1 or Plk1_H) and ( (CyclinB and Cdk1) or (CyclinA and not Wee1 and Cdc25A) )
Cdc25C *= f4N_DNA and Plk1 and ( (Cdc25B and not CHK1) or (CyclinB and Cdk1) )
Cdk1 *= CyclinB and Cdc25C and ( not CHK1 or (not Wee1 and Cdk1) )
pAPC *= (CyclinB and Cdk1 and Plk1) or (CyclinB and Cdk1 and pAPC) or (pAPC and Cdc20)
Cdc20 *= pAPC and not Emi1 and not Cdh1 and ( not Mad2 or ( not CyclinA and not (CyclinB and Cdk1) ) )
Cdh1 *= not (CyclinB and Cdk1) and not ( CyclinA and (Emi1 or Cdc25A) )
Replication *= not CAD and Pre_RC and ( (E2F1 and CyclinE and Cdc25A) or (Replication and CyclinA and Cdc25A and (E2F1 or not f4N_DNA) ) )
f4N_DNA *= not CAD and ( (Replication and ( (Pre_RC and CyclinA) or f4N_DNA) ) or (f4N_DNA and not Ect2) )
U_Kinetochores *= f4N_DNA and not Cdh1 and not A_Kinetochores and ( (CyclinB and Cdk1) or U_Kinetochores )
Mad2 *= U_Kinetochores and not A_Kinetochores
A_Kinetochores *= f4N_DNA and not Cdh1 and not (pAPC and Cdc20) and ( A_Kinetochores or (U_Kinetochores and Plk1 and CyclinB and Cdk1) )
Plk1_H *= Plk1 and FoxM1 and (Plk1_H or FoxO3 or FoxO1)
Ect2 *= f4N_DNA and Plk1_H and Cdh1 and not U_Kinetochores and not A_Kinetochores
Casp8 *= DR4_5 or Casp3
Casp2 *= Casp3 or (U_Kinetochores and Mad2 and not (CyclinB and Cdk1) )
MCL_1 *= not Casp3 and not Casp2 and (not GSK3 or (AKT_B and (ERK or not E2F1) ) ) and (not (Cdk1 and CyclinB and U_Kinetochores) )
BCLXL *= not Casp3 and (BCL2 or not BAD) and ( not U_Kinetochores or ( Plk1 and ( not (CyclinB and Cdk1) or (BCL2 and MCL_1) ) ) or  ( (BCL2 and MCL_1) and not (CyclinB and Cdk1) ) )
BCL2 *= not (Casp3 or BAD or BIM or BIK) and  ( not U_Kinetochores or (MCL_1 and BCLXL) or (Plk1 and (BCLXL or MCL_1 or not (Cdk1 and CyclinB) ) ) )
BAD *= Casp3 or not (AKT_H or AKT_B or ERK or S6K) or ( Casp8 and (not (AKT_B and ERK and S6K) and not (AKT_H and (AKT_B or ERK) ) ) )
BIK *= not (MCL_1 or BCLXL or BCL2)
BIM *= FoxO3 and GSK3 and not (ERK or MCL_1 or BCLXL or BCL2)
BID *= Casp8 or ( Casp2 and not (BCL2 or BCLXL or MCL_1) )
BAK *= (BID and (BIM or BIK or not (BCL2 and BCLXL and MCL_1) ) ) or ( (BIM or BIK) and not (BCLXL or MCL_1) )
BAX *= (BIM and ( (BID or BIK) or not (BCL2 and BCLXL and MCL_1) ) ) or ( (BID or BIK) and not (BCL2 or BCLXL) )
Cyto_C *= BAX or BAK
SMAC *= BAX or BAK
IAPs *= not SMAC or AKT_H
Casp9 *= Casp3 or (not IAPs and Cyto_C)
Casp3 *= (Casp9 and Casp8) or ( Casp3 and (Casp9 or Casp8) ) or ( not IAPs and (Casp9 or Casp8 or Casp3) )
CAD *= Casp3 and Casp9
ATR *= Replication
CHK1 *= ATR
Ca2 *= IP3
DAG *= PLCgamma
DR4_5 *= Trail
ERK *= MEK and not BIK
GF_High *= GF_High
IP3 *= PLCgamma
MEK *= RAF
SOS *= Grb2
Trail *= Trail
